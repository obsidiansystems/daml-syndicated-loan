-- | Acceptance of the loan origination agreement
module Scripts.Origination.Acceptance where

import DA.Foldable
import Daml.Script

import Daml.Finance.Interface.Settlement.Factory qualified as Settlement (F)
import Daml.Finance.Interface.Instrument.Bond.FixedRate.Factory qualified as FixedRate (F)
import Daml.Finance.Interface.Settlement.RouteProvider qualified as RouteProvider

import Scripts.Origination.Offer
import Util.Display
import Workflow.Origination

-- | Everything you need for multi-party loan acceptance.
-- Accepting the multi-party loan agreement requires the combination
-- of lender and borrower authorities delegated through various instruments
data MultipartyAcceptance = MultipartyAcceptance with
  agent : Party -- ^ Agent
  applicationProcess : ContractId LoanApplicationProcess
  acceptedLoan : ContractId AcceptedLoanOffer
  fundingSelection : FundingSelection
  routeProviderCid : ContractId RouteProvider.I
  settlementFactoryCid : ContractId Settlement.F
  fixedRateBondFactoryCid : ContractId FixedRate.F
  now : Time

-- | Create a multi-party loan agreement between the borrower, agent, and all
-- accepted lenders
multipartyAcceptance
  : MultipartyAcceptance
  -> Script (ContractId OriginatedLoanAgreement, OriginatedLoanAgreement)
multipartyAcceptance MultipartyAcceptance{..} = do
  log "-- Pending Loan Agreement --"
  -- Now that all parties have agreed to their individual terms, the agent constructs the final loan agreement
  -- and gathers the signatures that have been authorized.
  signedPendingLoanAgreement <- submit agent $
    exerciseCmd applicationProcess LoanApplicationProcess_CreatePendingLoanAgreement with
      borrower_commitment = acceptedLoan
      lender_commitments = fundingSelection.fundingSelection_selected
  logLedger agent signedPendingLoanAgreement

  -- Agent declines leftover funding interests
  forA_ fundingSelection.fundingSelection_declined $ \declinedCid -> do
    declined <- queryContractId agent declinedCid
    case declined of
      None -> pure ()
      Some dec -> do
        disclosed <- queryContractId agent dec.reportCid
        case disclosed of
          None -> pure ()
          Some dis -> log $ show dec.agent <> " declines offer from " <> show dec.participant
                            <> "\n" <> displayFundingInterestTerms dec dis
    submit agent $ exerciseCmd declinedCid FundingInterest_Decline

  -- Agent constructs the final, fully signed loan agreementâ€” ready for settlement
  origLoanAgreementCid <- submit agent $ exerciseCmd signedPendingLoanAgreement PendingLoanAgreement_Finalize
    with
      now
      routeProviderCid
      settlementFactoryCid
      fixedRateBondFactoryCid

  Some origLoanAgreement <- queryContractId agent origLoanAgreementCid
  log origLoanAgreement

  pure (origLoanAgreementCid, origLoanAgreement)
