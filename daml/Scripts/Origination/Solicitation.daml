-- | Solicitation of funding from agent to lenders
module Scripts.Origination.Solicitation where

import DA.Foldable
import Daml.Script

import Util.Display
import Workflow.Origination

data SolicitBids = SolicitBids with
  agent : Party
  possibleLenders : [Party] -- ^ Lenders who can submit a proposal to participate
  decliningLenders : [Party] -- ^ Lenders who will decline to participate
  underwritingResult : ContractId UnderwritingResult
  acceptedMandateLetter : ContractId AcceptedMandateLetter
  amount : Decimal -- ^ Total loan amount
  rate : RatePerAnnum -- ^ Interest rate

-- | Send solicitations to lenders to participate in the syndicated loan
-- and collect their responses
solicitBids
  : SolicitBids
  -> Script [(ContractId FundingInterest, FundingInterest)]
solicitBids SolicitBids{..} = do
  log "-- Lender solicitation --"
  -- Agent with underwriter's report and accepted mandate letter in hand reaches out to possible lenders
  forA_ possibleLenders $ \lender -> do
    makeFundingRequest agent underwritingResult acceptedMandateLetter lender
  -- Lender declines to make an offer
  forA_ decliningLenders $ \rue -> do
    rueFundingRequest <- makeFundingRequest agent underwritingResult acceptedMandateLetter rue
    log $ show rue <> " declines to participate"
    submit rue $ exerciseCmd rueFundingRequest FundingRequest_Reject
  -- Lenders make their offers
  fundingInterestCids <- forA possibleLenders $ \lender -> do
    [(fundingRequestCid, _fundingRequest)] <- getFundingRequestsFor lender
    cid <- submit lender $ makeFundingInterest fundingRequestCid (amount / 2.0 + 1.0, rate)
    -- To display all of the information we would like, we need to fetch a contract contained
    -- within the funding interest, unfortunately barring us from using the normal logging
    -- fixtures we've created.
    Some c <- queryContractId lender cid
    md <- queryContractId lender c.reportCid
    case md of
      None -> fail $ "Could not find contract id " <> show c.reportCid <> " with party " <> show lender
      Some d -> log $ displayFundingInterest c d
    pure cid
  fundingInterests <- forA fundingInterestCids $ \cid -> do
    Some fundingInterest <- queryContractId agent cid
    pure (cid, fundingInterest)
  pure fundingInterests

-- | Request from agent to a particular potential lender for funding
-- of the loan
makeFundingRequest
    : Party -- ^ agent
    -> ContractId UnderwritingResult
    -- ^ Proof that the agent has performed some underwriting (but DYOR!)
    -> ContractId AcceptedMandateLetter
    -- ^ Proof that the agent has a mandate to solicit for the borrower
    -> Party -- ^ Potential lender
    -> Script (ContractId FundingRequest)
makeFundingRequest agent underwriteCid mandateLetterCid lender = do
    mandateLetterTokenCid <- submit agent $ exerciseCmd mandateLetterCid AcceptedMandateLetter_CreateToken
    disclosedUnderwriting <- submit agent $ exerciseCmd underwriteCid UnderwritingResult_Disclose with
        disclose_to = lender
        mandateLetterTokenCid
    logLedger agent disclosedUnderwriting
    fundingRequest <- submit agent $
      exerciseCmd disclosedUnderwriting MakeFundingRequest with
        requestee = lender
    logLedger agent fundingRequest
    pure fundingRequest

getFundingRequestsFor : Party -> Script [(ContractId FundingRequest, FundingRequest)]
getFundingRequestsFor lender = query @FundingRequest lender

-- TODO: Actually make a decision about amount and rate
makeFundingInterest : ContractId FundingRequest -> (Decimal, RatePerAnnum) -> Commands (ContractId FundingInterest)
makeFundingInterest requestCid (amount, rate) = exerciseCmd requestCid FundingRequest_Respond with
    amount = (amount - 1.0)
    rate
